import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
  alias(libs.plugins.kotlin.jvm)
  alias(libs.plugins.dokka) apply false
  alias(libs.plugins.ktlint) apply false
  alias(libs.plugins.maven.publish) apply false
  alias(libs.plugins.plugin.publish) apply false
  alias(libs.plugins.versions)
  alias(libs.plugins.license)
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'
  id 'idea'
}

tasks.withType(Wrapper).configureEach {
  distributionType = Wrapper.DistributionType.ALL
}

idea {
  module {
    downloadSources = true
    downloadJavadoc = true
  }
}

subprojects {
  tasks.withType(Jar).configureEach {
    def dateFile = layout.buildDirectory.file('jar-manifest-date.txt').get().asFile
    if (!dateFile.exists()) {
      def date = DateTimeFormatter.ofPattern('EEE MMM dd HH:mm:ss zzz yyyy').
        format(ZonedDateTime.now())
      dateFile.parentFile.mkdirs()
      dateFile.text = date.trim()
    }

    manifest {
      attributes(
        'Created-By': POM_DEVELOPER_NAME,
        'Implementation-Title': POM_NAME,
        'Implementation-Version': VERSION_NAME,
        'Implementation-Vendor': POM_DEVELOPER_NAME,
        'Built-By': System.getProperty('user.name'),
        'Built-Date': dateFile.text.trim(),
        'Built-JDK': System.getProperty('java.version'),
        'Built-Gradle': gradle.gradleVersion)
    }
  }

  tasks.withType(KotlinJvmCompile).configureEach {
    compilerOptions {
      jvmTarget.set(JvmTarget.JVM_17)
      languageVersion.set(KotlinVersion.KOTLIN_2_0)
      apiVersion.set(KotlinVersion.KOTLIN_2_0)
      freeCompilerArgs.add("-progressive")
      freeCompilerArgs.add("-Xjsr305=strict")
      freeCompilerArgs.add("-Xemit-jvm-type-annotations")
      freeCompilerArgs.add("-Xassertions=jvm")
      freeCompilerArgs.add("-Xjvm-default=all")
    }
  }

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    configure(options) {
      compilerArgs << '-Xlint:all'
      compilerArgs << '-Xlint:-options'
      encoding = 'utf-8'
      fork = true
    }
  }

  tasks.withType(GroovyCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    configure(options) {
      compilerArgs << '-Xlint:all'
      compilerArgs << '-Xlint:-options'
      encoding = 'utf-8'
      fork = true
    }
  }

  tasks.withType(Test).configureEach {
    useJUnitPlatform() // Ensure JUnit Platform is used if you are using JUnit 5 or Spock 2.x

    testLogging {
      exceptionFormat = TestExceptionFormat.FULL
      showCauses = true
      showExceptions = true
      showStackTraces = true

      // Check if running on CI and set events accordingly
      events = System.getenv("CI") != null ?
        TestLogEvent.values() as Set :
        [TestLogEvent.FAILED, TestLogEvent.SKIPPED]
    }

    def maxWorkerCount = gradle.startParameter.maxWorkerCount
    maxParallelForks = (maxWorkerCount < 2) ? 1 : maxWorkerCount / 2
  }
}
