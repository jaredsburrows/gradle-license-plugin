plugins {
  alias(libs.plugins.kotlin.jvm)
  alias(libs.plugins.dokka)
  alias(libs.plugins.ktlint)
  alias(libs.plugins.maven.publish)
  alias(libs.plugins.plugin.publish)
  alias(libs.plugins.versions)
  alias(libs.plugins.license)
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'
}

group = GROUP
version = VERSION_NAME

import org.gradle.api.DefaultTask
import org.gradle.api.file.ConfigurableFileCollection
import org.gradle.api.file.DirectoryProperty
import org.gradle.api.plugins.JavaPluginExtension
import org.gradle.api.tasks.Classpath
import org.gradle.api.tasks.OutputDirectory
import org.gradle.api.tasks.TaskAction

abstract class CreateClasspathManifestTask extends DefaultTask {
  @Classpath
  abstract ConfigurableFileCollection getClasspath()

  @OutputDirectory
  abstract DirectoryProperty getOutputDir()

  @TaskAction
  void writeManifest() {
    def outputDir = outputDir.get().asFile
    outputDir.mkdirs()
    def manifestFile = new File(outputDir, 'plugin-classpath.txt')
    def entries = classpath.files.collect { it.path }.toSet().toList()
    entries.sort()
    manifestFile.text = entries.join("\n")
  }
}

tasks.register('createClasspathManifest', CreateClasspathManifestTask) {
  def javaPluginExtension = project.extensions.getByType(JavaPluginExtension)
  def mainRuntimeClasspath = javaPluginExtension.sourceSets.getByName('main').runtimeClasspath

  classpath.from(mainRuntimeClasspath)
  classpath.from(configurations.pluginTestKitClasspath)
  outputDir.set(layout.buildDirectory.dir(name))
}

sourceSets {
  test {
    resources {
      srcDir(layout.buildDirectory.dir('createClasspathManifest'))
    }
  }
}

tasks.named('processTestResources') {
  dependsOn(tasks.named('createClasspathManifest'))
}

configurations {
  pluginTestKitClasspath {
    canBeConsumed = false
    canBeResolved = true
    extendsFrom testRuntimeOnly
  }
}

dependencies {
  compileOnly gradleApi()
  compileOnly libs.android.plugin

  implementation platform(libs.kotlin.bom)
  implementation libs.kotlin.stdlib
  implementation libs.kotlinx.html
  implementation libs.moshi
  implementation libs.maven.model

  testRuntimeOnly libs.android.plugin

  testImplementation localGroovy()
  testImplementation gradleTestKit()
  testImplementation libs.spock, { exclude module: 'groovy-all' } // Use localGroovy()
  testImplementation libs.xmlunit
  testImplementation libs.commons
}

gradlePlugin {
  website = POM_URL
  vcsUrl = POM_SCM_URL
  plugins {
    licensePlugin {
      id = PLUGIN_NAME
      implementationClass = PLUGIN_NAME_CLASS
      displayName = POM_NAME
      description = POM_DESCRIPTION
      tags.set(['license'])
    }
  }
}
