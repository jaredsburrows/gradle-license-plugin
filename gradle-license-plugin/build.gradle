plugins {
  alias(libs.plugins.kotlin.jvm)
  alias(libs.plugins.dokka)
  alias(libs.plugins.ktlint)
  alias(libs.plugins.maven.publish)
  alias(libs.plugins.plugin.publish)
  alias(libs.plugins.versions)
  alias(libs.plugins.license)
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'
}

group = GROUP
version = VERSION_NAME

def mainClasspath = sourceSets.main.runtimeClasspath
def testClasspath = sourceSets.test.runtimeClasspath

tasks.register('createClasspathManifest') {
  def outputDir = layout.buildDirectory.file(name).get().asFile

  inputs.files mainClasspath
  outputs.dir outputDir

  doLast {
    outputDir.mkdirs()
    // Combine both main and test plugin classpaths
    def set = new HashSet<String>()
    mainClasspath.files.forEach {
      set.add(it.path)
    }
    testClasspath.files.forEach {
      set.add(it.path)
    }
    def list = new ArrayList<String>(set)
    new File(outputDir, 'plugin-classpath.txt').text = String.join("\n", list)
  }
}

dependencies {
  compileOnly gradleApi()
  compileOnly libs.android.plugin

  implementation platform(libs.kotlin.bom)
  implementation libs.kotlin.stdlib
  implementation libs.kotlinx.html
  implementation libs.moshi
  implementation libs.maven.model

  testRuntimeOnly files(createClasspathManifest)
  testRuntimeOnly libs.android.plugin
  testImplementation platform(libs.junit.bom)
  testRuntimeOnly libs.junit.platform.launcher
  testRuntimeOnly libs.junit.jupiter.engine
  testRuntimeOnly libs.junit.vintage.engine

  testImplementation localGroovy()
  testImplementation gradleTestKit()
  testImplementation libs.spockcore, { exclude module: 'groovy-all' } // Use localGroovy()
  testImplementation libs.spock.junit4
  testImplementation libs.xmlunit
  testImplementation libs.commons
}

gradlePlugin {
  website = POM_URL
  vcsUrl = POM_SCM_URL
  plugins {
    licensePlugin {
      id = PLUGIN_NAME
      implementationClass = PLUGIN_NAME_CLASS
      displayName = POM_NAME
      description = POM_DESCRIPTION
      tags.set(['license'])
    }
  }
}
